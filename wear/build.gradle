apply plugin: 'com.android.application'


android {
    signingConfigs {
        playRelease
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId "com.kuxhausen.huemore"
        minSdkVersion 20
        targetSdkVersion 23
        versionCode 81 // must match the value in mobile
    }
    buildTypes {
        release {
            signingConfig signingConfigs.playRelease
        }
    }
    sourceSets {
        release.res.srcDirs = ['res', '../../trademarks']
    }
}

def Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.playRelease.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.playRelease.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.playRelease.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.playRelease.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.google.android.support:wearable:1.1.0'
    compile 'com.google.android.gms:play-services-wearable:7.5.0'
}
